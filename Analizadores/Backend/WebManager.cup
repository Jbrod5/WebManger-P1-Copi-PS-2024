package com.jbrod.webmanager_server.analyzer;

import java_cup.runtime.*;
import com.jbrod.webmanager_server.webmanager.WebsiteMgr;
import com.jbrod.webmanager_server.webmanager.WebManager;

import com.jbrod.webmanager_server.webcomponents.WebPage;
import com.jbrod.webmanager_server.webcomponents.WebComponent;
import com.jbrod.webmanager_server.webcomponents.EtiquetaWebComponent;

import com.jbrod.webmanager_server.webcomponents.ImagenWebComponent;
import com.jbrod.webmanager_server.webcomponents.ParrafoWebComponent;
import com.jbrod.webmanager_server.webcomponents.TituloWebComponent;
import com.jbrod.webmanager_server.webcomponents.VideoWebComponent;

import java.util.LinkedList;




parser code {:

    //Manejador de sitios web.
    private WebsiteMgr websiteMgr = new WebsiteMgr();
    private WebManager webManager;
    private LinkedList<WebComponent> tags;

    //Parametros para construir componentes
    private String origen; 
    private String align; 
    private String height; 
    private String width;
    private String text; 
    private String color;  


    // Connect this parser to a scanner!
    public WebParser(WebLexer lex, WebManager manager) {
	    super(lex);
        webManager = manager;
    }

    public void syntax_error(Symbol cur_token) {
        System.out.println("Simbolo con error:" + symbl_name_from_id(cur_token.sym));
        System.out.println("Linea " + cur_token.left);
        System.out.println("Columna " + cur_token.right);
        if (expected_token_ids().isEmpty()) {
            System.out.println("ya no se esperaba ningun simbolo");
        }
    }
    /*public void unrecovered_syntax_error(Symbol cur_token) {
        System.out.println("Error irrecuperable sobrecargado");
    }*/
:}





/* - - - - - - - - - - - - - - - - - - - - Terminales - - - - - - - - - - - - - - - - - - - - */

/* Valores */
terminal String PRM_VAL_ID, PRM_VAL_FCH, PRM_VAL_TIT, ATR_VAL_CENT, ATR_VAL_IZQD, ATR_VAL_DERE, ATR_VAL_JUST, ATR_VAL_COLH, ATR_VAL_TEXT, ATR_VAL_INTG, CLASS_TYPE_TIT, CLASS_TYPE_PAR, CLASS_TYPE_IMG, CLASS_TYPE_VID, CLASS_TYPE_MEN;
/* Parametros */
terminal String PARAMETROS_OP, PARAMETROS_CL, PARAM_TYPE_ID, PARAM_TYPE_TIT, PARAM_TYPE_SIT, PARAM_TYPE_PAD, PARAM_TYPE_USC, PARAM_TYPE_FCR, PARAM_TYPE_FMD, PARAM_TYPE_USM, PARAM_TYPE_PAG, PARAM_TYPE_CLS, PARAMETRO_CL;
/* Atributos */
terminal String ATRIBUTOS_OP, ATRIBUTOS_CL, ATR_TYPE_TXT, ATR_TYPE_CLR, ATR_TYPE_ALN, ATR_TYPE_ORI, ATR_TYPE_ALT, ATR_TYPE_ANC, ATRIBUTO_CL;
/* Acciones */
terminal String ACCIONES_OP, ACCIONES_CL, ACC_ADD_WBST, ACC_DEL_WBST, ACC_ADD_PAGW, ACC_DEL_PAGW, ACC_MOD_PAGW, ACC_ADD_COMP, ACC_DEL_COMP, ACC_MOD_COMP, ACCION_CL;
/* Etiquetas */
terminal String ETIQUETAS_OP, ETIQUETAS_CL, ETIQUETA;


/* - - - - - - - - - - - - - - - - - - -  No terminales - - - - - - - - - - - - - - - - - - - */
// Inicio
non terminal String instruccion, accion, accion_rec, acciones;

// Parametros
non terminal String prm_val_id, prm_val_uc, prm_val_usm, prm_val_fch, prm_val_fmd, prm_val_tit, prm_val_sit, prm_val_pad, prm_val_pag;

// Agregar componentes 
non terminal String class_val_tit, class_val_par, class_val_img, class_val_vid, class_val_men, class_type, prm_val_cls;

// Etiquetas
non terminal String etiqueta, etiqueta_op, etiquetas;

// Atributos
non terminal String atr_val_txt, aln_cent, aln_izqd, aln_dere, aln_just, alineacion, atr_val_aln, atr_val_clr, atr_val_ori, atr_val_alt, atr_val_anc, atributo, atributo_op, atributos;

// Acciones
non terminal String accion_add_wbst, accion_del_wbst, prm_val_pad_op,accion_add_pagw, accion_del_pagw, accion_mod_pagw, accion_add_comp, accion_del_comp, accion_mod_comp;


//Auxiliares
non terminal String atr_val_op_txt;



/* - - - - - - - - - - - - - - - - - - -  Producciones - - - - - - - - - - - - - - - - - - -  */


// Inicio
start with instruccion;

instruccion ::=  acciones | accion;
accion      ::=  accion_add_wbst | accion_del_wbst | accion_add_pagw | accion_del_pagw | accion_mod_pagw | accion_add_comp | accion_del_comp | accion_mod_comp;
accion_rec  ::=  accion accion_rec | accion;
acciones    ::=  ACCIONES_OP accion_rec ACCIONES_CL;

// Parametros
prm_val_id  ::= PARAM_TYPE_ID    PRM_VAL_ID:id    PARAMETRO_CL {: RESULT = id; :};
prm_val_pad ::= PARAM_TYPE_PAD   PRM_VAL_ID:id    PARAMETRO_CL {: RESULT = id; :};
prm_val_uc  ::= PARAM_TYPE_USC   PRM_VAL_ID:id    PARAMETRO_CL {: RESULT = id; :};
prm_val_usm ::= PARAM_TYPE_USM   PRM_VAL_ID:id    PARAMETRO_CL {: RESULT = id; :};
prm_val_sit ::= PARAM_TYPE_SIT   PRM_VAL_ID:id    PARAMETRO_CL {: RESULT = id; :};
prm_val_fch ::= PARAM_TYPE_FCR   PRM_VAL_FCH:fc   PARAMETRO_CL {: RESULT = fc; :};
prm_val_fmd ::= PARAM_TYPE_FMD   PRM_VAL_FCH:fc   PARAMETRO_CL {: RESULT = fc; :};

prm_val_tit ::= PARAM_TYPE_TIT   PRM_VAL_TIT:ti   PARAMETRO_CL {: RESULT = ti; :};


prm_val_pag ::= PARAM_TYPE_PAG   PRM_VAL_ID:id    PARAMETRO_CL {: RESULT = id; :};












//agregar componentes
class_val_tit ::= CLASS_TYPE_TIT;  
class_val_par ::= CLASS_TYPE_PAR;
class_val_img ::= CLASS_TYPE_IMG;
class_val_vid ::= CLASS_TYPE_VID;
class_val_men ::= CLASS_TYPE_MEN;
class_type    ::= class_val_tit {: RESULT = "TITULO";  :}
                | class_val_par {: RESULT = "PARRAFO"; :}
                | class_val_img {: RESULT = "IMAGEN";  :}
                | class_val_vid {: RESULT = "VIDEO";   :}
                | class_val_men {: RESULT = "MENU";    :};
prm_val_cls   ::= PARAM_TYPE_CLS  class_type:type  PARAMETRO_CL {: RESULT = type; :};

//Etiquetas
etiqueta    ::= ETIQUETA:et {: 
                                if(tags == null){
                                    tags = new LinkedList<>();
                                }
                                EtiquetaWebComponent etiqueta = new EtiquetaWebComponent(et); 
                                tags.add(((WebComponent)etiqueta));
                                for(int i = 0; i < tags.size(); i++){
                                    System.out.println(tags.get(i).getId());
                                }
                                RESULT = et; 
                            :};
etiqueta_op ::= etiqueta etiqueta_op:et {: RESULT += et; :}| etiqueta:et {: RESULT += et; :}; 
etiquetas   ::= ETIQUETAS_OP etiqueta_op:et ETIQUETAS_CL {: RESULT = et; :};

//Atributos
atr_val_op_txt ::= PRM_VAL_TIT:tit  {: RESULT = tit;  :} 
                 | ATR_VAL_TEXT:txt {: RESULT = txt;  :};  
atr_val_txt ::= ATR_TYPE_TXT atr_val_op_txt:text ATRIBUTO_CL {: RESULT = text; :};

aln_cent    ::= ATR_VAL_CENT:cent {: RESULT = cent; :};
aln_izqd    ::= ATR_VAL_IZQD:iz {:RESULT = iz;:};
aln_dere    ::= ATR_VAL_DERE:der {: RESULT = der; :};
aln_just    ::= ATR_VAL_JUST:js {: RESULT = js; :};
alineacion  ::= aln_cent  :op {: RESULT = op; :}
                | aln_izqd:op {: RESULT = op; :} 
                | aln_dere:op {: RESULT = op; :} 
                | aln_just:op {: RESULT = op; :}
                | PRM_VAL_TIT:op{:RESULT = op;:}; 
atr_val_aln ::= ATR_TYPE_ALN alineacion:aln ATRIBUTO_CL{: RESULT = aln; :};
          
atr_val_clr ::= ATR_TYPE_CLR ATR_VAL_COLH:col ATRIBUTO_CL{: RESULT = col; :};
 
atr_val_ori ::= ATR_TYPE_ORI ATR_VAL_TEXT:txt ATRIBUTO_CL {: RESULT = txt; :}
            |   ATR_TYPE_ORI PRM_VAL_TIT:txt  ATRIBUTO_CL {: RESULT = txt; :};
       
atr_val_alt ::= ATR_TYPE_ALT ATR_VAL_INTG:ent ATRIBUTO_CL {: RESULT = ent; :};
    
atr_val_anc ::= ATR_TYPE_ANC ATR_VAL_INTG:ent ATRIBUTO_CL {: RESULT = ent;  :};

// FALTA MENU

atributo    ::= atr_val_txt:txt   {: text   = txt; :} 
                | atr_val_aln:aln {: align  = aln; :}
                | atr_val_clr:clr {: color  = clr; :}
                | atr_val_ori:ori {: origen = ori; :}
                | atr_val_alt:alt {: height = alt; :} 
                | atr_val_anc:anc {: width  = anc; :};
atributo_op ::= atributo atributo_op | atributo;
atributos   ::= ATRIBUTOS_OP atributo_op ATRIBUTOS_CL;

















// Acciones
accion_add_wbst ::=  ACC_ADD_WBST PARAMETROS_OP prm_val_id:id prm_val_uc:uc prm_val_fch:fch prm_val_fmd:fmd prm_val_usm:usm PARAMETROS_CL ACCION_CL
                    {: webManager.createWebsite(id); :};
accion_del_wbst ::=  ACC_DEL_WBST prm_val_id:id ACCION_CL
                    {: webManager.deleteWebsite(id); :};




prm_val_pad_op  ::=  prm_val_pad:pad {: RESULT = pad; :} | /* epsilon */ {: RESULT = "$index"; :};
accion_add_pagw ::=  ACC_ADD_PAGW PARAMETROS_OP    prm_val_id:id     prm_val_tit:tit   prm_val_sit:sit    prm_val_pad_op:pad     prm_val_uc:uc     prm_val_fch:fch    prm_val_fmd:fmd     prm_val_usm:usm     PARAMETROS_CL etiquetas ACCION_CL
                    {:
                         WebPage newPage = new WebPage(id, tit, sit, pad, uc, fch, fmd, usm);
                         newPage.replaceTags(tags);
                         webManager.addPage(newPage);
                         tags = null;   
                    :};
accion_del_pagw ::=  ACC_DEL_PAGW prm_val_id:id ACCION_CL
                    {:
                        webManager.deletePage(id);
                    :};
accion_mod_pagw ::=  ACC_MOD_PAGW  PARAMETROS_OP prm_val_id:id prm_val_tit:tit PARAMETROS_CL etiquetas:et ACCION_CL
                    {:
                        webManager.modifyPage(id, tit, tags);
                        tags = null; 
                    :};






accion_add_comp ::=  ACC_ADD_COMP  PARAMETROS_OP prm_val_id:id  prm_val_pag:idpag  prm_val_cls:cls  PARAMETROS_CL atributos ACCION_CL
                    {:

                        WebComponent toAdd = null; 
                        //Evaluar la clase de componente
                        switch(cls){
                            case "TITULO":
                                toAdd = new TituloWebComponent(id, text, align, color); 
                                break; 
                            case "PARRAFO":
                                toAdd = new ParrafoWebComponent(id, text, align, color);
                                break; 
                            case "IMAGEN":
                                toAdd = new ImagenWebComponent(id, origen, align, height, width);
                                break; 
                            case "VIDEO":
                                toAdd = new VideoWebComponent(id, origen, width, height);
                                break; 
                            case "MENU":
                                toAdd = null; 
                                break; 
                            default:
                                toAdd = null; 
                                break;  
                        } 

                        //Agregar el componente
                        if(toAdd != null){
                            webManager.addComponent(idpag, toAdd);
                        }
                    :};

accion_del_comp ::=  ACC_DEL_COMP PARAMETROS_OP prm_val_id:id_comp prm_val_pag:id_pag  PARAMETROS_CL  ACCION_CL
                {: webManager.deleteComponent(id_pag, id_comp); :};

accion_mod_comp ::=  ACC_MOD_COMP  PARAMETROS_OP    prm_val_id:id   prm_val_pag:id_pag   prm_val_cls:cls    PARAMETROS_CL atributos ACCION_CL
                {:
                    WebComponent toAdd = null;
                    switch(cls){
                            case "TITULO":
                                toAdd = new TituloWebComponent(id, text, align, color); 
                                break; 
                            case "PARRAFO":
                                toAdd = new ParrafoWebComponent(id, text, align, color);
                                break; 
                            case "IMAGEN":
                                toAdd = new ImagenWebComponent(id, origen, align, height, width);
                                break; 
                            case "VIDEO":
                                toAdd = new VideoWebComponent(id, origen, width, height);
                                break; 
                            case "MENU":
                                toAdd = null; 
                                break; 
                            default:
                                toAdd = null; 
                                break;  
                        } 

                        //Agregar el componente
                        if(toAdd != null){
                            webManager.modifyComponent(id_pag, toAdd);
                        } 

                :};